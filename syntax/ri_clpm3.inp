TITLE:
RI-CLPM with equal effects constraints
DATA:
FILE = "/Users/zekai/WorkPlace/RI_CLPM/MPLUS/ri_clpm3.dat";
 
VARIABLE:
NAMES = CA_1 CA_2 CA_3 CA_4 INT_1 INT_2 INT_3 INT_4; 
 MISSING=.;
 
ANALYSIS:
ESTIMATOR = MLR;
MODEL:

  ! 随机截距因子
  RI_INT BY INT_1-INT_4@1;
  RI_CA BY CA_1-CA_4@1;
  
  ! 随机截距间协方差
  RI_INT WITH RI_CA;
  
  ! 计算个体内变量
  w_INT1 BY INT_1@1;
  w_INT2 BY INT_2@1;
  w_INT3 BY INT_3@1;
  w_INT4 BY INT_4@1;
  
  w_CA1 BY CA_1@1;
  w_CA2 BY CA_2@1;
  w_CA3 BY CA_3@1;
  w_CA4 BY CA_4@1;
  
  ! 设置个体内变量均值为0
  [w_INT1-w_INT4@0];
  [w_CA1-w_CA4@0];
  
  ! 固定各时刻的指标负荷量
  INT_1@0; INT_2@0; INT_3@0; INT_4@0;
  CA_1@0; CA_2@0; CA_3@0; CA_4@0;
  
  ! 所有自回归效应相等
  w_INT2 ON w_INT1 (ar);
  w_INT3 ON w_INT2 (ar);
  w_INT4 ON w_INT3 (ar);
  w_CA2 ON w_CA1 (ar);
  w_CA3 ON w_CA2 (ar);
  w_CA4 ON w_CA3 (ar);
  
  ! 所有交叉滞后效应相等
  w_INT2 ON w_CA1 (cl);
  w_INT3 ON w_CA2 (cl);
  w_INT4 ON w_CA3 (cl);
  w_CA2 ON w_INT1 (cl);
  w_CA3 ON w_INT2 (cl);
  w_CA4 ON w_INT3 (cl);
  
  ! 同期关联
  w_INT1 WITH w_CA1;
  w_INT2 WITH w_CA2;
  w_INT3 WITH w_CA3;
  w_INT4 WITH w_CA4;
  
  ! 随机截距方差
  RI_INT;
  RI_CA;
OUTPUT:
STDYX patterns
