TITLE:
Two-variable CLPM - Internalizing and Maltreatment with Covariates
DATA:
FILE = "/Users/zekai/WorkPlace/RI_CLPM/MPLUS/clpm_int_ca_cov.dat";
 
VARIABLE:
NAMES = CA_1 CA_2 CA_3 CA_4 INT_1 INT_2 INT_3 INT_4 age center gender race
     income_group education employment_status; 
 MISSING=.;
 
ANALYSIS:
ESTIMATOR = MLR;
MODEL:

  ! 自回归路径 - 时间稳定性约束
  INT_2 on INT_1 (a1);
  INT_3 on INT_2 (a1);
  INT_4 on INT_3 (a1);
  
  CA_2 on CA_1 (a2);
  CA_3 on CA_2 (a2);
  CA_4 on CA_3 (a2);
  
  ! INT与CA之间的交叉滞后路径 - 时间稳定性约束
  INT_2 on CA_1 (b1);
  INT_3 on CA_2 (b1);
  INT_4 on CA_3 (b1);
  
  CA_2 on INT_1 (b2);
  CA_3 on INT_2 (b2);
  CA_4 on INT_3 (b2);
  
  ! 初始时刻的协方差
  INT_1 with CA_1;
  
  ! 后续时刻的同期相关
  INT_2 with CA_2;
  INT_3 with CA_3;
  INT_4 with CA_4;
  
  ! 协变量对所有时间点的影响 - 人口统计学变量
  INT_1 CA_1 on age center gender race income_group education employment_status;
  INT_2 CA_2 on age center gender race income_group education employment_status;
  INT_3 CA_3 on age center gender race income_group education employment_status;
  INT_4 CA_4 on age center gender race income_group education employment_status;

OUTPUT:
STDYX patterns
